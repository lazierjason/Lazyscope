<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:control="com.lazyscope.control.*"
			   width="400" creationComplete="onCreationComplete(event)" close="onClose()" backgroundColor="0xF3F1E9"
			   title="Add a subscription">
	<fx:Script>
		<![CDATA[
			import com.lazyscope.Base;
			import com.lazyscope.entry.Blog;
			
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;

			public static var subscribeInstance:SubscribeWindow;
			public static var isShowing:Boolean = false;
			
			public static function show():void
			{
				if (!subscribeInstance) {
					subscribeInstance = new SubscribeWindow;
				}
				
				PopUpManager.addPopUp(subscribeInstance, Base.app, true);
				PopUpManager.centerPopUp(subscribeInstance);
				isShowing = true;
				
				subscribeInstance.init();
			}
			
			public static function hide():void
			{
				if (subscribeInstance)
					subscribeInstance.onClose();
			}
			
			public function onCreationComplete(event:Event):void
			{
				addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown, false, 0, true);
//				url.addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown, false, 0, true);
			}
			
			public function init():void
			{
				url.text = '';
				url.setFocus();
			}
			
			public function onClose():void
			{
				PopUpManager.removePopUp(this);
				isShowing = false;
			}
			
			override public function set enabled(value:Boolean):void
			{
				if (url) url.enabled = value;
				if (btnAdd) btnAdd.enabled = value;
				super.enabled = value;
			}
			
			public function addClick():void
			{
				if (url.text == null || url.text.length <= 0) return;
				
				url.enabled = btnAdd.enabled = false;
				
				Base.feed.registerFeed('http://'+(url.text.replace(/^(https?|feed):\/\//i, '')), function(b:Blog):void {
					url.enabled = btnAdd.enabled = true;
					if (!b) {
						url.setFocus();
						url.selectRange(0, url.text.length);
					}else
						hide();
				});
			}
			
			public function onKeyDown(event:KeyboardEvent):void
			{
				if (event.keyCode == 27) {
					event.stopPropagation();
					event.preventDefault();
					onClose();
				}
			}


			protected function opml_clickHandler(event:MouseEvent):void
			{
				onClose();
				Base.showOPMLImport();
			}


			protected function url_keyDownHandler(event:KeyboardEvent):void
			{
				if (event.keyCode == 13) {
					event.stopPropagation();
					event.preventDefault();
					addClick();
				}
			}

		]]>
	</fx:Script>
	
	<s:VGroup width="100%" top="10" bottom="10" paddingLeft="15" paddingRight="15" gap="7">
		<s:Label width="100%" text="Type in the address of any website you like, and press Add." />
		<s:HGroup width="100%" verticalAlign="middle">
			<s:Label fontSize="13" color="0x333333" text="URL" />
			<s:TextInput id="url" maxChars="500" editable="true" width="100%" height="24"  borderColor="0x9A9A9A" keyDown="url_keyDownHandler(event)" />
			<control:Btn id="btnAdd" skinClass="com.lazyscope.control.BtnSkin" width="67" height="24" iconPrefix="add" click="addClick()" />
		</s:HGroup>
		<s:Group width="100%">
			<s:Label fontSize="12" color="0x0279B4" text="Import feeds (OPML)" textDecoration="underline" right="0" top="5" useHandCursor="true" buttonMode="true" mouseChildren="true" click="opml_clickHandler(event)" />
		</s:Group>
	</s:VGroup>

</s:TitleWindow>
